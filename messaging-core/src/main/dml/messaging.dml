package org.fenixedu.messaging.core.domain;

valueType org.fenixedu.messaging.core.domain.MessageStoragePolicy as MessageStoragePolicy {
	externalizeWith {
		String serialize();
	}
	internalizeWith internalize();
}

class MessagingSystem {
}

class Sender {
	public boolean htmlEnabled;
	public String name;
	public String address;
	public String replyTo;
	public MessageStoragePolicy policy;
	public boolean attachmentsEnabled;
}

class Message {
	protected LocalizedString subject;
	protected LocalizedString textBody;
	protected LocalizedString htmlBody;
	protected Locale preferredLocale;
	protected DateTime created;
	protected String replyTo;
	protected String singleBccs;
	protected String singleTos;
}

class MessageTemplate {
	protected String id;
	public LocalizedString subject;
	public LocalizedString textBody;
	public LocalizedString htmlBody;
}

class MessageDispatchReport {
	protected DateTime startedDelivery;
	public DateTime finishedDelivery;
	public int totalCount;
	public int deliveredCount;
	public int invalidCount;
	public int failedCount;
}

class MessageFile extends .org.fenixedu.bennu.io.domain.GenericFile {
}

relation MessageFiles {
    protected Message playsRole message {
        multiplicity 0..*;
    }
    protected .org.fenixedu.bennu.io.domain.GenericFile playsRole file {
        multiplicity 0..*;
    }
}

relation SenderFiles {
    protected Sender playsRole sender {
        multiplicity 1..1;
    }
    protected MessageFile playsRole uploadedFile {
        multiplicity 0..*;
    }
}

relation MessagingSystemRoot {
	protected .org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
		multiplicity 1..1;
	}
	public MessagingSystem playsRole messagingSystem {
		multiplicity 0..1;
	}
}

relation MessagingSystemTemplate {
	protected MessagingSystem playsRole messagingSystem {
		multiplicity 1..1;
	}
	protected MessageTemplate playsRole template {
		multiplicity *;
	}
}

relation MessagingSystemSender {
	protected MessagingSystem playsRole messagingSystem {
		multiplicity 1..1;
	}
	protected Sender playsRole sender {
		multiplicity *;
	}
}

relation MessagingSystemSystemSender {
	protected MessagingSystem playsRole rootForSystemSender {
		multiplicity 0..1;
	}
	protected Sender playsRole systemSender {
		multiplicity 0..1;
	}
}

relation MessagingSystemMessage {
	protected MessagingSystem playsRole messagingSystem {
		multiplicity 1..1;
	}
	protected Message playsRole message {
		multiplicity *;
	}
}

relation MessagingSystemMessagePending {
	protected MessagingSystem playsRole messagingSystemFromPendingDispatch {
		multiplicity 0..1;
	}
	protected Message playsRole messagePendingDispatch {
		multiplicity *;
	}
}

relation MessageDispatchReports {
	protected Message playsRole message {
		multiplicity 1..1;
	}
	protected MessageDispatchReport playsRole dispatchReport {
		multiplicity 0..1;
	}
}

relation SenderPersistentGroupMembers {
	protected Sender playsRole senderFromMember {
		multiplicity *;
	}
	protected .org.fenixedu.bennu.core.domain.groups.PersistentGroup playsRole memberGroup {
		multiplicity 1..1;
	}
}

relation SenderPersistentGroupRecipients {
	protected Sender playsRole senderFromRecipient {
		multiplicity *;
	}
	protected .org.fenixedu.bennu.core.domain.groups.PersistentGroup playsRole recipient {
		multiplicity *;
	}
}

relation SenderMessage {
	protected Sender playsRole sender {
		multiplicity 1..1;
	}
	protected Message playsRole message {
		multiplicity *;
	}
}

relation MessageRecipientTo {
	protected Message playsRole messageFromTo {
		multiplicity *;
	}
	protected .org.fenixedu.bennu.core.domain.groups.PersistentGroup playsRole to {
		multiplicity *;
	}
}

relation MessageRecipientCc {
	protected Message playsRole messageFromCc {
		multiplicity *;
	}
	protected .org.fenixedu.bennu.core.domain.groups.PersistentGroup playsRole cc {
		multiplicity *;
	}
}

relation MessageRecipientBcc {
	protected Message playsRole messageFromBcc {
		multiplicity *;
	}
	protected .org.fenixedu.bennu.core.domain.groups.PersistentGroup playsRole bcc {
		multiplicity *;
	}
}

relation MessageUser {
	protected Message playsRole message {
		multiplicity *;
	}
	protected .org.fenixedu.bennu.core.domain.User playsRole creator {
		multiplicity 0..1;
	}
}

relation MessagingSystemOptOutAvailableGroup {
	protected MessagingSystem playsRole messagingSystemOptOutAvailable {
		multiplicity 0..1;
	}
	protected .org.fenixedu.bennu.core.domain.groups.PersistentGroup playsRole persistentOptOutAvailableGroup {
		multiplicity 0..1;
	}
}

relation MessagingSystemOptedOutGroup {
	protected MessagingSystem playsRole messagingSystemOptedOut {
		multiplicity 0..1;
	}
	protected .org.fenixedu.bennu.core.domain.groups.PersistentGroup playsRole persistentOptedOutGroup {
		multiplicity 0..1;
	}
}
